buildscript {
  repositories {
    mavenCentral()
    maven {
      name = 'fabricmc.net'
      url = 'https://maven.fabricmc.net'
    }
    maven {
      name = 'files.minecraftforge.net'
      url = 'https://files.minecraftforge.net/maven'
    }
    maven {
      name = 'jitpack.io'
      url = 'https://jitpack.io'
    }
  }
  dependencies {
    classpath "io.github.Chocohead:fabric-loom:$loom"
  }
}

apply plugin: 'java'
apply plugin: 'fabric-loom'

archivesBaseName = rootProject.name
group = 'io.github.insomniakitten'
version = mod
sourceCompatibility = 1.8

minecraft {
  refmapName = 'mixins/fovlock/refmap.json'
}

repositories {
  maven {
    name = 'projectlombok.org/edge-releases'
    url = 'https://projectlombok.org/edge-releases'
  }
}

dependencies {
  minecraft "com.mojang:minecraft:$game"
  mappings "net.fabricmc:yarn:$game+$mappings:enigma@zip"
  modCompile "net.fabricmc:fabric-loader:$loader"
  modCompile "net.fabricmc:fabric:$api"
  implementation "net.fabricmc:yarn:$game+$mappings"
  implementation "com.google.code.findbugs:jsr305:$findbugs"
  implementation "org.projectlombok:lombok:$lombok"
  annotationProcessor "org.projectlombok:lombok:$lombok"
}

processResources {
  def metadata = [
    'fabric.mod.json', 'pack.mcmeta',
    'mixins/fovlock/mixins.json'
  ]
  sourceSets.main.resources.srcDirs.with {
    from(it) {
      include metadata
      expand version: version, refmap: minecraft.refmapName
    }
    from(it) {
      exclude metadata
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
    '-Xlint:all',
    '-XprintRounds',
    '-XprintProcessorInfo',
    '-encoding', 'UTF-8',
    '-Xmaxerrs', Integer.MAX_VALUE,
    '-Xmaxwarns', Integer.MAX_VALUE
  ]
}
